grammar forml.Forml with org.eclipse.xtext.common.Terminals

generate forml "http://www.Forml.forml"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Models:
	models+=Model+
	//- Check that one and only one Model (the Root Model) is not extended
;
	
Model:
	'Model' Name
	('extends' extendedModels+=ExtendedModel (',' extendedModels+=ExtendedModel)*)? 
	ItemizedDefinition	statements+=Statement*
	/// Check that Model is not in extension closure
	/// Warning if 'name' begins with a lower case, with a quick fix
	/// Warning if 'name' begins with a ° or an _
	/// Warning if a begin-end block is not provided with an 'endName', with a quick fix
	/// Check that if an 'endName' is provided, it is equal to 'name', with a quick fix
;

fragment Name:
	name=ID comments+=STRING*
;

ExtendedModel:
	model=[Model] comments+=STRING*
;

fragment ItemizedDefinition:
	(	':' statements+=Statement	//- Check that is not a comment
	|	 'begin' statements+=Statement* blockEnd?='end' endName=ID? ';' 
	|	';'
	)
;

Statement:
	partialModel=PartialModel
|	simpleClass=SimpleClass
|	enumeration=Enumeration
|	object=Object
|	definition=Definition
|	objectGlobalDefinition=[Object | QualifiedName] 'is' value=Value ';'
|	comments+=STRING+
;

Declaration:
	PartialModel
|	ClassDeclaration
|	Object
;

ClassDeclaration:
	SimpleClass
|	Enumeration
//|	QuantityClass
//|	SetClass
;

PartialModel:
	'partial' 'Model' Name ItemizedDefinition
	/// Warning if 'name' begins with a lower case, with a quick fix
	/// Warning if 'name' begins with a °
	/// Warning if a begin-end block is not provided with an 'endName', with a quick fix
	/// Check that if an 'endName' is provided, it is equal to 'name', with a quick fix
;

SimpleClass:
	ClassModifier 'Class' (determiner=Determiner)? Name
	ExtendedClasses? 
	ItemizedDefinition
	/// Check that 'name' is not in the closure of 'extendedClasses'
	/// Warning if 'name' begins with a lower case, with a quick fix
	/// Warning if 'name' begins with a ° or an _
	/// Warning if a begin-end block is not provided with an 'endName', with a quick fix
	/// Check that if an 'endName' is provided, it is equal to 'name', with a quick fix
	//- Check that 'statements' contains no partial model declaration / definition
;

Enumeration:
	ClassModifier 'Enumeration' states=States (determiner=Determiner)? Name
	ExtendedClasses? 
	ItemizedDefinition
	/// Check that 'name' is not in the closure of 'extendedClasses'
	/// Warning if 'name' begins with a lower case, with a quick fix
	/// Warning if 'name' begins with a ° or an _
	/// Warning if a begin-end block is not provided with an 'endName', with a quick fix
	/// Check that if an 'endName' is provided, it is equal to 'name', with a quick fix
	//- Check that 'statements' contains no partial model declaration / definition
;

fragment ClassModifier:
	(main?='main' | external?='external')? & (private?='private')? & (abstract?='abstract')?
;

fragment ExtendedClasses:
	'extends' extendedClasses+=ClassReference (',' extendedClasses+=ClassReference)*
;

ClassReference:
(	preDefinedClass=PredefinedClass
|	definedClass=[ClassDeclaration]
)	comments+=STRING*
;

PredefinedClass:
	'Boolean'  | 'Integer'    | 'Real'      | 'String'      | 'Event'
|	'Property' | 'Assumption' | 'Objective' | 'Requirement' | 'Guarantee'
|	'Guard'    | 'Object'
;

States:
	'[' states+=State
(	list?=',' states+=State (',' states+=State)* 
|	product?='&' states+=State ('&' states+=State)+
)	']'
;

State:
(	states=States 				// Composite state on the fly
|	enumeration=[Enumeration]	// Composite state on existing enumeration
)?	name=ID
	comments+=STRING*
;

Determiner: '#' | '@' | '~' | '^' | '!' | '?';
 
Object:
	(main?='main'? | (external?='external'? & private?='private'? & (constant?='constant' | fixed?='fixed')?))
	classes+=ClassReference (',' classes+=ClassReference)*
	(determiner=Determiner)? Name
	(is?='is' value=Value ';' valueComments+=STRING* | ItemizedDefinition)
	/// Warning if 'name' begins with an upper case, with a quick fix
	/// Warning if a begin-end block is not provided with an 'endName', with a quick fix
	/// Verify that if a begin-end block is provided with an 'endName', 
	/// 	it is equal to 'name', with a quick fix
	// Verify that the same 'name' is not declared with different types
	//  Verify that 'statements' does not contain any partial model
;

Definition:
	item=[Declaration | QualifiedName] comments+=STRING* ItemizedDefinition
	/// Warning if a begin-end block is not provided with an 'endName', with a quick fix
	/// Check that if an 'endName' is provided, it is equal to 'item.name', with a quick fix
;

Value:
	{Value} true?='true'?
|	{Value} false?='false'?
|	{Value} real=Float?
|	{Value} string=STRING?
|	{Value} reference=[Object | QualifiedName]?
;

Float hidden():  
(	whole=INT ('.' (decimal=INT | exp=EXPONENT)?)?
|	'.' (decimal=INT | exp=EXPONENT)
|	exp=EXPONENT
)
;

QualifiedName hidden(): ID('.' ID)*;

@Override
terminal ID: ('a'..'z'|'A'..'Z'|'°') ('a'..'z'|'A'..'Z'|'_'|'°'|'0'..'9')*;

terminal EXPONENT: ('0'..'9')+ ('E' | 'e') ('+' | '-')? ('0'..'9')+;

@Override
terminal INT returns ecore::EInt: ('0'..'9')+;


