grammar formlS.FormlS with org.eclipse.xtext.common.Terminals
// This grammar specifies only the FormL syntax

generate formlS "http://www.FormlS.formlS"

MockUp:
	// A mock-up is a sequence of models, with at least one model
	comments += STRING*
	models+=Model+;
	
Model:
	ModelHeader (ModelDefinitionBlock | ';')
	statements+=Statement*
;

fragment ModelHeader:
	'Model' name=ID 
	comments += STRING*
	('extends' extended+=ModelName (',' extended+=ModelName)*)?
	comments += STRING*
;

fragment ModelDefinitionBlock:
	':' statements+=Statement
|	'begin' statements+=Statement* 'end' (endName=ModelName)? ';'
;	

ModelName:	
	id=ID
;

Statement:
	decl=Declaration
|	block=Definition
//|	inst=Instruction
;

Declaration:
	type=Type
	determiner=('!' | '#' | '@' | '^' | '~' | '?')? 
	name=ID
	(ItemDefinitionBlock | ';')
;

Definition:
	ItemName
	ItemDefinitionBlock
;

Type:
	'Integer'
;

ItemName:	
	determiner=('!' | '#' | '@' | '^' | '~' | '?')? 
	id=ID
;

fragment ItemDefinitionBlock:
	':' statements+=Statement
|	'begin' statements+=Statement* 'end' (endName=ItemName)? ';'
|	'is' value=INT ';'
;	

