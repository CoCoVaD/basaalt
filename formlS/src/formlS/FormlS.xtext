grammar formlS.FormlS with org.eclipse.xtext.common.Terminals
// This grammar specifies only the FormL syntax

generate formlS "http://www.FormlS.formlS"

MockUp:
	// A mock-up is a sequence of models, with at least one model
	comments += STRING*
	models+=Model+
	comments += STRING*
;
	
// ========== Model ==================================================
Model:
	// A model is composed of a model declaration, possibly combined
	// with a model definition block, and possibly followed by statements 
	// contributing to the definition of the model
	ModelDeclaration (DefinitionBlock | ';')
	statements+=Statement*
;

fragment ModelDeclaration:
	'Model' name=ID 
	comments += STRING*
	('extends' extended+=[Model] (',' extended+=[Model])*)?
	comments += STRING*
;

fragment DefinitionBlock:
	':' statements+=Statement
|	'begin' statements+=Statement* 'end' (endName=ID)? ';'
;	

fragment GlobalDefinition:
	'is' value=INT ';'
;

Statement:
	sectionDecl=Section
|	classDecl=DefinedClass
|	objectDecl=Object
|	contractDecl=Contract
|	acknowledment=Acknowledgment
|	encroachmentDecl=Encroachment
//|	tlDecl=TempotalLocator
|	refinement=Refinement
//|	inst=Instruction
;


// ========== Section ==================================================
Section:
	'Section' name=ID
	comments+=STRING*
	(DefinitionBlock | ';')
;

// ========== Refinement ==================================================
Refinement:
	'refined'? Determiner? refined=[Refinable]         comments += STRING* DefinitionBlock
|	'refined'? Determiner? refined=[GloballyRefinable] comments += STRING* GlobalDefinition
|	'refined'  Determiner? refined=[Refinable]         comments += STRING* ';'
;

Refinable:
	Section | DefinedClass | Object | Contract | Encroachment
;

GloballyRefinable:
	DefinedClass | Object
;

// ========== Class ===========================================
PredefinedClass:
	'Boolean' | 'Integer' | 'Real' | 'String'
|	'Event'
|	'Property' | 'Assumption' | 'Objective' | 'Requirement' | 'Guarantee' | 'Guard'
|	'Object'
;

DefinedClass:
	((private?='private' | main?='main' | external?='external')? & (abstract?='abstract')?)
	('Class' | enumeration?='Enumeration' states=States comments+=STRING*) 
	Determiner? name=ID
	mySetIndication=SetIndication?
	Parameters?
	comments+=STRING*
	('extends' extended+=Class (',' extended+=Class)*)?
	comments += STRING*
	(DefinitionBlock | ';')
;

States:
	'[' states+=State ((',' states+=State)+ | ('&' states+=State)+) ']'
;

State:
	(enumeration=[DefinedClass] | states=States)? id=ID comments+=STRING*
;

Class:
	(	definedClass=[DefinedClass] Arguments? 
	| 	predefinedClass=PredefinedClass
	|	automaton?='Automaton' states=States comments+=STRING*
	) setIndication=SetIndication?
;

fragment Determiner:
	determiner=('!' | '#' | '@' | '^' | '~' | '?')
;

fragment Variability:
	(fixed?='fixed' | constant?='constant')
;

fragment Cardinal:
	ln=Expression ('..' (ub=Expression | infinity?='%'))?
;

fragment Parameters:
	'(' parameters+=Parameter (',' parameters+=Parameter)* ')'
;

Parameter:
	class=Class 
	Determiner? id=ID 
	comments+=STRING*
;

fragment Arguments:
	'(' arguments+=Expression (',' arguments+=Expression)* ')'
;

Expression:
	value=INT	// temporary
;

SetIndication:
	{SetIndication} domain?='$'? '{' (Determiner? & Variability? & Cardinal?) '}'
;

// ========== Object =================================================
Object:
 	(	main?='main'
	|	external?='external'? & private?='private'? & common?='common'? & Variability?
	)
	(classes+=Class | '(' classes+=Class (',' classes+=Class)* ')')
	Determiner? name=ID
	comments += STRING*
	Parameters?
	comments+=STRING*
	(DefinitionBlock | GlobalDefinition | ';')
;

// ========== Contract =================================================
Contract:
	'Contract' name=ID 
	comments += STRING*
	'(' parties+=Party (',' parties+=Party)* ')'
	comments += STRING*
	(DefinitionBlock | ';')
;

Party:
//	(class=[DefinedClass] | object=[Object]) comments+=STRING*
	party=ID comments+=STRING*
;

Acknowledgment:
	'acknowledged' id=[Interface] comments+=STRING* ';'
;

Interface:
	Contract
|	Encroachment
;

// ========== Encroachment =================================================
Encroachment:
	'Encroachment' name=ID 
	comments += STRING*
	'(' origin=Party ',' target=Party ')'
	comments += STRING*
	(DefinitionBlock | ';')
;
